set(CPACK_PACKAGE_NAME "dingo")
set(CPACK_PACKAGE_VENDOR "Dingo Project")
set(CPACK_PACKAGE_CONTACT "https://github.com/kisungyou/Dingo/issues")
set(CPACK_PACKAGE_DESCRIPTION_SUMMARY "Dingo: header-only C++ numerical library backed by Eigen")
set(CPACK_RESOURCE_FILE_README "${CMAKE_CURRENT_SOURCE_DIR}/INSTALL.txt")
set(CPACK_PACKAGE_HOMEPAGE_URL "https://github.com/kisungyou/Dingo")

if(DEFINED ENV{GITHUB_REF_NAME} AND NOT "$ENV{GITHUB_REF_NAME}" STREQUAL "")
  string(REGEX REPLACE "^v" "" _dingo_version "$ENV{GITHUB_REF_NAME}")
  set(CPACK_PACKAGE_VERSION "${_dingo_version}")
else()
  set(CPACK_PACKAGE_VERSION "${PROJECT_VERSION}")
endif()

if(CMAKE_SYSTEM_PROCESSOR MATCHES "^(x86_64|amd64|AMD64)$")
  set(DINGO_PACKAGE_ARCH "x86_64")
  set(CPACK_DEBIAN_PACKAGE_ARCHITECTURE "amd64")
  set(CPACK_RPM_PACKAGE_ARCHITECTURE "x86_64")
elseif(CMAKE_SYSTEM_PROCESSOR MATCHES "^(aarch64|arm64|ARM64)$")
  set(DINGO_PACKAGE_ARCH "arm64")
  set(CPACK_DEBIAN_PACKAGE_ARCHITECTURE "arm64")
  set(CPACK_RPM_PACKAGE_ARCHITECTURE "aarch64")
else()
  set(DINGO_PACKAGE_ARCH "${CMAKE_SYSTEM_PROCESSOR}")
  set(CPACK_DEBIAN_PACKAGE_ARCHITECTURE "${CMAKE_SYSTEM_PROCESSOR}")
  set(CPACK_RPM_PACKAGE_ARCHITECTURE "${CMAKE_SYSTEM_PROCESSOR}")
endif()

set(CPACK_VERBATIM_VARIABLES YES)

if(APPLE)
  set(CPACK_GENERATOR "productbuild;TGZ")
  set(CPACK_PACKAGING_INSTALL_PREFIX "/usr/local")
  set(CPACK_PACKAGE_DIRECTORY "${CMAKE_BINARY_DIR}/packages")
  set(CPACK_PACKAGE_FILE_NAME "dingo-${CPACK_PACKAGE_VERSION}-macos-${DINGO_PACKAGE_ARCH}")
  set(CPACK_RESOURCE_FILE_LICENSE "${CMAKE_CURRENT_SOURCE_DIR}/packaging/macos/LICENSE.txt")
  set(CPACK_PRODUCTBUILD_IDENTIFIER "io.github.kisungyou.dingo")
  set(CPACK_PRODUCTBUILD_DOMAINS TRUE)
  set(CPACK_ARCHIVE_FILE_NAME "dingo-${CPACK_PACKAGE_VERSION}-macos-${DINGO_PACKAGE_ARCH}")
elseif(WIN32)
  set(CPACK_GENERATOR "WIX;ZIP")
  set(CPACK_PACKAGE_FILE_NAME "dingo-${CPACK_PACKAGE_VERSION}-windows-${DINGO_PACKAGE_ARCH}")
  set(CPACK_RESOURCE_FILE_LICENSE "${CMAKE_CURRENT_SOURCE_DIR}/LICENSE")
  set(CPACK_PACKAGE_INSTALL_DIRECTORY "Dingo")
  set(CPACK_WIX_UPGRADE_GUID "F242A12A-A639-48CE-B9C8-4A3BF44A355A")
  set(CPACK_WIX_PRODUCT_GUID "*")
  set(CPACK_WIX_PATCH_FILE "${CMAKE_CURRENT_SOURCE_DIR}/packaging/wix/dingo.wxs")
  set(CPACK_ARCHIVE_FILE_NAME "dingo-${CPACK_PACKAGE_VERSION}-windows-${DINGO_PACKAGE_ARCH}")
else()
  set(CPACK_GENERATOR "DEB;RPM;TGZ")
  set(CPACK_PACKAGE_FILE_NAME "dingo-${CPACK_PACKAGE_VERSION}-linux-${DINGO_PACKAGE_ARCH}")
  set(CPACK_RESOURCE_FILE_LICENSE "${CMAKE_CURRENT_SOURCE_DIR}/LICENSE")
  set(CPACK_PACKAGING_INSTALL_PREFIX "/usr")
  set(CPACK_DEBIAN_FILE_NAME DEB-DEFAULT)
  set(CPACK_DEBIAN_PACKAGE_MAINTAINER "Dingo Project")
  set(CPACK_DEBIAN_PACKAGE_SECTION "devel")
  set(CPACK_DEBIAN_PACKAGE_CONTROL_EXTRA
    "${CMAKE_CURRENT_SOURCE_DIR}/packaging/linux/postinst;${CMAKE_CURRENT_SOURCE_DIR}/packaging/linux/prerm")
  set(CPACK_RPM_PACKAGE_LICENSE "MIT")
  set(CPACK_RPM_PACKAGE_GROUP "Development/Libraries")
  set(CPACK_RPM_POST_INSTALL_SCRIPT_FILE "${CMAKE_CURRENT_SOURCE_DIR}/packaging/linux/postinst")
  set(CPACK_RPM_PRE_UNINSTALL_SCRIPT_FILE "${CMAKE_CURRENT_SOURCE_DIR}/packaging/linux/prerm")
  set(CPACK_ARCHIVE_FILE_NAME "dingo-${CPACK_PACKAGE_VERSION}-linux-${DINGO_PACKAGE_ARCH}")
endif()

include(CPack)
